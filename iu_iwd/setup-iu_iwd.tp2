BACKUP ~iu_iwd/backup~ // location to store files for backup
SUPPORT ~https://www.gibberlings3.net/forums/forum/152-icewind-dale-mod-roundup/~ // url displayed if install fails

README ~iu_iwd/readme-iu_iwd.html~
VERSION ~v6~

AUTO_TRA ~iu_iwd/languages/%s~

LANGUAGE ~English~  
  ~english~  
  ~iu_iwd/languages/english/setup.tra~
LANGUAGE ~Russian (by prowler, Accolon, Hawkmoon, Skaramush, Aldanis Darkwood)~ 
  ~russian~ 
  ~iu_iwd/languages/english/setup.tra~
  ~iu_iwd/languages/russian/setup.tra~
LANGUAGE ~Francais (par Lothringen)~ 
  ~french~ 
  ~iu_iwd/languages/english/setup.tra~
  ~iu_iwd/languages/french/setup.tra~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// item upgrade                                     \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @0 DESIGNATED 0
REQUIRE_PREDICATE GAME_IS ~iwd how totlm iwd-in-bg2~ @1

OUTER_SET "iwd-in-bg2" = 0
ACTION_IF GAME_IS ~iwd-in-bg2~ THEN BEGIN // if IWD-in-BG2 instead of true IWD...
  OUTER_SET "iwd-in-bg2" = 1
END

COMPILE ~iu_iwd/dlg/gond.d~  // girdle of gond upgrade
COMPILE ~iu_iwd/dlg/pelts.d~ // pelt upgrades

EXTEND_BOTTOM ~ar2004.bcs~   ~iu_iwd/baf/ar2004.baf~   // set var if stoutward is in the game
EXTEND_BOTTOM ~ar3001.bcs~   ~iu_iwd/baf/ar3001.baf~   // add tools
EXTEND_BOTTOM ~ar6003.bcs~   ~iu_iwd/baf/ar6003.baf~   // add symbol if stoutward is in the game
EXTEND_BOTTOM ~kuorrick.bcs~ ~iu_iwd/baf/kuorrick.baf~ // orrick purges spare pelts

// add new icons
COPY ~iu_iwd/bam/cdechain.bam~ ~override~ // elven chain mail
     ~iu_iwd/bam/cdharmor.bam~ ~override~ // yeti hide armor
     ~iu_iwd/bam/cdmitham.bam~ ~override~ // mithril hammer
     ~iu_iwd/bam/cdrdagg.bam~  ~override~ // returning throwing dagger
     ~iu_iwd/bam/cdrdart.bam~  ~override~ // returning dart
     ~iu_iwd/bam/cdstout.bam~  ~override~ // improved stoutward
     ~iu_iwd/bam/cdtools.bam~  ~override~ // gnomish tools
     ~iu_iwd/bam/cdwuff.bam~   ~override~ // winter wuff cloak

COPY_EXISTING ~ar2004.are~ ~override~
              ~ar3001.are~ ~override~
              ~ar6003.are~ ~override~
  WRITE_ASCIIE 0x94 ~%SOURCE_RES%~ #8
  BUT_ONLY

// elven chain mail
COPY ~iu_iwd/itm/cdechain.itm~ ~override~
  SAY 0x08 @1024 // unidentified name
  SAY 0x0c @1024 // identified name
  SAY 0x50 @1025 // unidentified description
  SAY 0x54 @1025 // identified description

// only available in a store, so...
COPY_EXISTING ~beltgon.itm~ ~override~
  WRITE_SHORT 0x42 0 // lore
  BUT_ONLY

// imp girdle of gond
COPY_EXISTING ~beltgon.itm~ ~override/cdgond.itm~
  SAY 0x50 @1012  // unidentified description
  SAY 0x54 @1012  // identified description
  LPF ALTER_EFFECT INT_VAR match_opcode = 90 parameter1 = 15 END // boost lockpicking bonus 10 > 15
  LPF ALTER_EFFECT INT_VAR match_opcode = 91 parameter1 = 10 END // boost find traps bonus 5 > 10

// yeti hide armor
COPY ~iu_iwd/itm/cdharmor.itm~ ~override~
  SAY 0x08 @1000 // unidentified name
  SAY 0x0c @1000 // identified name
  SAY 0x50 @1001 // unidentified description
  SAY 0x54 @1001 // identified description

// mithril hammer
COPY ~iu_iwd/itm/cdmitham.itm~ ~override~
  SAY 0x08 @1033 // unidentified name
  SAY 0x0c @1034 // identified name
  SAY 0x50 @1035 // unidentified description
  SAY 0x54 @1036 // identified description

// returning throwing dagger
COPY ~iu_iwd/itm/cdrdagg.itm~ ~override~
  SAY 0x08 @1026 // unidentified name
  SAY 0x0c @1026 // identified name
  SAY 0x50 @1027 // unidentified description
  SAY 0x54 @1027 // identified description

// improved stoutward
COPY_EXISTING ~stout.itm~ ~override/cdstout.itm~
  WRITE_ASCII 0x3a ~cdstout~ #8
  WRITE_SHORT 0x42 0 // lore
  SAY 0x50 @1013  // unidentified description
  SAY 0x54 @1013  // identified description
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 18 target = 1 parameter1 = 10 parameter2 = 3 timing = 2 END // +10 hp when equipped
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 44 target = 1 parameter1 = 1 timing = 2 END // +1 str when equipped

// returning dart
COPY ~iu_iwd/itm/cdrdart.itm~ ~override~
  SAY 0x08 @1028 // unidentified name
  SAY 0x0c @1028 // identified name
  SAY 0x50 @1029 // unidentified description
  SAY 0x54 @1029 // identified description

// gnomish tools
COPY ~iu_iwd/itm/cdtools.itm~ ~override~
  SAY 0x08 @1037 // unidentified name
  SAY 0x0c @1037 // identified name
  SAY 0x50 @1038 // unidentified description
  SAY 0x54 @1038 // identified description

// winter wuff cloak
COPY ~iu_iwd/itm/cdwuff.itm~ ~override~
  SAY 0x08 @1031 // unidentified name
  SAY 0x0c @1031 // identified name
  SAY 0x50 @1032 // unidentified description
  SAY 0x54 @1032 // identified description
  PATCH_IF ("%iwd-in-bg2%" = 1) BEGIN
    LPF ALTER_EFFECT INT_VAR match_opcode = 206 STR_VAR match_resource = spin971 resource = spin246 END // winter wolf innate is different resource
  END

COPY_EXISTING ~kuork2.sto~ ~override~
  ADD_STORE_ITEM ~cdrdagg~ AFTER ~wandmis~ #0 #0 #0 ~IDENTIFIED~ #1 // returning dagger
  BUT_ONLY
    
COPY_EXISTING ~shlehlan.sto~ ~override~
  ADD_STORE_ITEM ~cdechain~ AFTER ~thistle~ #0 #0 #0 ~IDENTIFIED~ #1 // elven chain
  ADD_STORE_ITEM ~cdrdart~  AFTER ~udart3b~ #0 #0 #0 ~IDENTIFIED~ #1 // returning dart
  BUT_ONLY

/////                                                  \\\\\
///// how                                              \\\\\
/////                                                  \\\\\

ACTION_IF NOT FILE_EXISTS_IN_GAME ~ar9100.are~ THEN BEGIN // no how

  COMPILE ~iu_iwd/dlg/conlan.d~ // stoutward upgrade

END ELSE BEGIN

  OUTER_SET dplanar_totlm = "-100"
  ACTION_IF ((MOD_IS_INSTALLED ~iwdfixpack/setup-iwdfixpack.tp2~ ~0~) AND         // fixpack is installed
             (FILE_EXISTS ~iwdfixpack/languages/english/dplanar.tra~))  BEGIN // and it's >= v7

    OUTER_SET dplanar_totlm = STATE_WHICH_SAYS 100 IN ~iwdfixpack/languages/%s/dplanar.tra~ FROM ~dplanar~ // set state number for totlm question hub

  END

  ACTION_IF ("%iwd-in-bg2%" = 1) THEN BEGIN // if IWD-in-BG2 instead of true IWD...

    APPEND ~tooltip.2da~ ~cdgloom           -1           86194        86231~
    APPEND ~tooltip.2da~ ~cdmyst            86216        95501       -1~
    APPEND ~tooltip.2da~ ~cynicis           -1           86194        86231~

  END ELSE BEGIN // if true IWD

    APPEND ~tooltip.2da~ ~cdgloom           -1           12094        12131~
    APPEND ~tooltip.2da~ ~cdmyst            12116        21401       -1~
    APPEND ~tooltip.2da~ ~cynicis           -1           12094        12131~
  
  END

  APPEND ~tooltip.2da~ ~cdxbowbm          -1           cdxbowbmtext       -1~
  APPEND ~tooltip.2da~ ~cddaen            cddaentext        -1          -1~
  APPEND ~tooltip.2da~ ~cddaen2           cddaentext        -1          -1~
  COPY_EXISTING ~tooltip.2da~ ~override~
    REPLACE ~cddaentext~   @1045
    REPLACE ~cdxbowbmtext~ @1046

  COMPILE ~iu_iwd/dlg/cddaen.d~     // myrloch vale upgrade
  COMPILE ~iu_iwd/dlg/conlan_how.d~ // stoutward and white dragon scale upgrade
    USING ~iu_iwd/languages/%s/conlan.tra~
  COMPILE ~iu_iwd/dlg/gloom.d~      // gloom upgrade
  ACTION_IF dplanar_totlm >= 0 BEGIN
    COMPILE ~iu_iwd/dlg/muller.d~     // bren muller's xbow upgrade
            ~iu_iwd/dlg/muller_fp7.d~ EVALUATE_BUFFER USING ~iu_iwd/languages/%s/muller.tra~
  END ELSE BEGIN
    COMPILE ~iu_iwd/dlg/muller.d~     // bren muller's xbow upgrade
  END

  ACTION_IF ("%iwd-in-bg2%" = 1) THEN BEGIN // if IWD-in-BG2 instead of true IWD...
  
    COPY ~iu_iwd/dlg/remorrhaz.d~ ~iu_iwd/dlg/remorrhaz.d~
      REPLACE_TEXTUALLY ~#10098~ ~#84198~

    COPY ~iu_iwd/dlg/mystery.d~   ~iu_iwd/dlg/mystery.d~
      REPLACE_TEXTUALLY ~School(\(Player[1-6]\),Necromancer)~ ~Kit(\1,MAGESCHOOL_NECROMANCER)~
  
  END
  
  COMPILE ~iu_iwd/dlg/remorrhaz.d~  // remorrhaz shell upgrade
  COMPILE ~iu_iwd/dlg/mystery.d~    // mystery of the dead upgrade

  COMPILE ~iu_iwd/baf/cddaen.baf~  // daen winterbrush's override script
  COMPILE ~iu_iwd/baf/cdgloom.baf~ // gloom cutscene
  COMPILE ~iu_iwd/baf/cdyoung.baf~ // young rage cutscene
  
  ACTION_IF ("%iwd-in-bg2%" = 1) THEN BEGIN // if IWD-in-BG2 instead of true IWD...

    COPY_EXISTING ~cdgloom.bcs~ ~override~
      DECOMPILE_BCS_TO_BAF
        REPLACE_TEXTUALLY ~"kieran"~ ~"kierannye"~
      COMPILE_BAF_TO_BCS
      BUT_ONLY
  
  END

  EXTEND_BOTTOM ~ar4004.bcs~   ~iu_iwd/baf/ar4004.baf~   // add ballad of daen
  EXTEND_BOTTOM ~ar5004.bcs~   ~iu_iwd/baf/ar5004.baf~   // telanis speaking post-cutscene
  EXTEND_BOTTOM ~ar8010.bcs~   ~iu_iwd/baf/ar8010.baf~   // add extra emotion: hope to malavon's lair
  EXTEND_BOTTOM ~idedion.bcs~  ~iu_iwd/baf/idedion.baf~  // purge spare armor
  EXTEND_BOTTOM ~lwkieran.bcs~ ~iu_iwd/baf/lwkieran.baf~ // kieran cutscene trippin'
  EXTEND_BOTTOM ~lwmurd.bcs~   ~iu_iwd/baf/lwmurd.baf~   // murdaugh speaking post-cutscene

  // add new icons
  COPY ~iu_iwd/bam/cdqbow.bam~   ~override~ // quiver of plenty
       ~iu_iwd/bam/cdqsling.bam~ ~override~ // bag of plenty
       ~iu_iwd/bam/cdqxbow.bam~  ~override~ // case of plenty
       ~iu_iwd/bam/cdrelm.bam~   ~override~ // remorrhaz shell helmet
       ~iu_iwd/bam/cdrem.bam~    ~override~ // remorrhaz shell
       ~iu_iwd/bam/cdremg.bam~   ~override~ // remorrhaz shell (ground)
       ~iu_iwd/bam/cdscale.bam~  ~override~ // white dragon scale armor
       ~iu_iwd/bam/cdscales.bam~ ~override~ // white dragon scales

  COPY_EXISTING ~ar5004.are~ ~override~
    WRITE_ASCIIE 0x94 ~%SOURCE_RES%~ #8
    BUT_ONLY

  // daen winterbrush
  ACTION_IF ("%iwd-in-bg2%" = 1) THEN BEGIN // if IWD-in-BG2 instead of true IWD...

    COPY ~iu_iwd/cre/conv_cddaen.cre~ ~override/cddaen.cre~
    
  END ELSE BEGIN

    COPY ~iu_iwd/cre/cddaen.cre~ ~override~
  
  END
  
  COPY_EXISTING ~cddaen.cre~ ~override~
    SAY 0x08 @1042 // unidentified name
    SAY 0x0c @1042 // identified name
  
  // add extra emotion: hope scroll to vaarglan
  COPY_EXISTING ~vaarglan.cre~ ~override~
    ADD_CRE_ITEM ~scemot~ #0 #0 #0 ~NONE~ ~INV~
    BUT_ONLY
  
  // add scales to icasa
  COPY_EXISTING ~icasa.cre~ ~override~
    ADD_CRE_ITEM ~cdscales~ #0 #0 #0 ~UNSTEALABLE~ ~INV~
    BUT_ONLY

  // add remorrhaz shells to remorrhazs
  COPY_EXISTING ~remorha.cre~  ~override~
                ~remorhas.cre~ ~override~
                ~remorhh.cre~  ~override~
                ~rndremor.cre~ ~override~
    ADD_CRE_ITEM ~cdrem~ #0 #0 #0 ~UNSTEALABLE~ ~INV~
    BUT_ONLY

  // ballad of daen winterbrush
  COPY ~iu_iwd/itm/cddaen.itm~   ~override~
    SAY 0x08 @1043 // unidentified name
    SAY 0x0c @1043 // identified name
    WRITE_LONG  0x34 30 // price
    WRITE_SHORT 0x42  0 // lore
    SAY 0x50 @1044 // unidentified description
    SAY 0x54 @1044 // identified description
    
  // post-upgrade ballad
  COPY_EXISTING ~cddaen.itm~ ~override/cddaen2.itm~
    LPF DELETE_EFFECT INT_VAR match_opcode = 67 END // doesn't summon any more

  // make upgraded cynicism
  COPY_EXISTING ~cynicis.itm~ ~override/cdgloom.itm~
    SAY 0x08 @1016 // unidentified name
    SAY 0x0c @1016 // identified name
    WRITE_SHORT 0x42 0 // lore
    SAY 0x50 @1017 // unidentified description
    SAY 0x54 @1017 // identified description
    PATCH_IF iwd-in-bg2 BEGIN
      LPF DELETE_EFFECT INT_VAR match_opcode = 139 END // delete display stringheads
    END ELSE BEGIN  
      LPF DELETE_EFFECT INT_VAR match_opcode = 283 END // delete display stringheads
    END  
    LPF ALTER_ITEM_HEADER INT_VAR header_type = 1 thac0_bonus = 4 END
    LPF ALTER_ITEM_HEADER INT_VAR header_type = 3 charges = 2 END 

  // improved myrloch vale
  COPY_EXISTING ~myrloch.itm~ ~override/cdmyr.itm~
    WRITE_SHORT 0x42 0 // lore
    SAY 0x50 @1019  // unidentified description
    SAY 0x54 @1019  // identified description
    LPF ALTER_EFFECT INT_VAR match_opcode = 21 parameter1 = 30 END // boost lore bonus from 20 to 30
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 6 target = 1 parameter1 = 1 timing = 2 END // +1 CHR when equipped
    
  ACTION_IF ("%iwd-in-bg2%" = 1) THEN BEGIN // if IWD-in-BG2 instead of true IWD...

    // add protection from divine FoD in iwd-in-bg2
    COPY_EXISTING ~mystery.itm~ ~override~
      LPF CLONE_EFFECT INT_VAR match_opcode = 206 STR_VAR match_resource = spwi713 resource = sppr708 END // add protection from divine FoD
      BUT_ONLY
      
  END

  // improved mystery of the dead
  COPY_EXISTING ~mystery.itm~ ~override/cdmyst.itm~
    WRITE_SHORT 0x42 0 // lore
    SAY 0x50 @1018  // unidentified description
    SAY 0x54 @1018  // identified description
    READ_LONG   0x64 abil_off
    READ_SHORT  0x68 orig_abil_num
    READ_LONG   0x6a fx_off
    READ_SHORT  0x70 fx_num
    PATCH_IF orig_abil_num BEGIN
      READ_SHORT  (abil_off + 0x1e + ((orig_abil_num - 1) * 0x38)) last_fx_num
      READ_SHORT  (abil_off + 0x20 + ((orig_abil_num - 1) * 0x38)) last_fx_idx
      SET new_abil_fx_idx = last_fx_idx + last_fx_num
    END ELSE BEGIN
      SET new_abil_fx_idx = fx_num
    END
    // manually insert two new abilities
    INSERT_BYTES  (fx_off       ) 0x38             // new ability
      WRITE_BYTE  (fx_off       ) 3                // magical
      WRITE_BYTE  (fx_off + 0x01) 1                // ID to use
      WRITE_SHORT (fx_off + 0x02) 3                // in item slots
      PATCH_IF (iwd-in-bg2 = 0) BEGIN
        WRITE_ASCII (fx_off + 0x04) ~sppr516b~       // ability icon
      END ELSE BEGIN
        WRITE_ASCII (fx_off + 0x04) ~#ppr516b~       // ability icon
      END
      WRITE_SHORT (fx_off + 0x0c) 5                // target: caster
      WRITE_SHORT (fx_off + 0x0e) 1                // range
      WRITE_SHORT (fx_off + 0x20) new_abil_fx_idx  // fx index
      WRITE_SHORT (fx_off + 0x22) 1                // num charges: 1
      WRITE_BYTE  (fx_off + 0x24) 3                // recharges daily
      WRITE_BYTE  (fx_off + 0x27) 8                // after resting
      WRITE_SHORT (fx_off + 0x2a) 1                // projectile: none
    READ_ASCII fx_off clone (56)  
    INSERT_BYTES  (fx_off       ) 0x38             // new ability
      WRITE_ASCIIE fx_off ~%clone%~ #56            // clone in previous new ability values
      WRITE_ASCII (fx_off + 0x05) ~ppr502b~ #7     // ability icon
      WRITE_SHORT (fx_off + 0x0c) 1                // target: living actor
    WRITE_SHORT 0x68 (THIS + 2)    // update ability number field
    WRITE_LONG  0x6a (THIS + 0x70) // update effects offset
    LPF ADD_ITEM_EFFECT INT_VAR header = (orig_abil_num + 1) opcode = 146 target = 2 parameter1 = 9 timing = 1 STR_VAR resource = sppr502 END
    PATCH_IF (iwd-in-bg2 = 0) BEGIN
      LPF ADD_ITEM_EFFECT INT_VAR header = (orig_abil_num + 2) opcode = 146 target = 1 parameter1 = 11 timing = 1 STR_VAR resource = sppr516 END
    END ELSE BEGIN  
      LPF ADD_ITEM_EFFECT INT_VAR header = (orig_abil_num + 2) opcode = 146 target = 1 parameter1 = 11 timing = 1 STR_VAR resource = sppr519 END
    END  

  // quiver of plenty
  COPY ~iu_iwd/itm/cdqbow.itm~   ~override~
    SAY 0x08 @1002 // unidentified name
    SAY 0x0c @1002 // identified name
    SAY 0x50 @1003 // unidentified description
    SAY 0x54 @1003 // identified description
  
  // bag of plenty
  COPY ~iu_iwd/itm/cdqsling.itm~ ~override~
    SAY 0x08 @1006 // unidentified name
    SAY 0x0c @1006 // identified name
    SAY 0x50 @1007 // unidentified description
    SAY 0x54 @1007 // identified description
  
  // case of plenty
  COPY ~iu_iwd/itm/cdqxbow.itm~  ~override~
    SAY 0x08 @1004 // unidentified name
    SAY 0x0c @1004 // identified name
    SAY 0x50 @1005 // unidentified description
    SAY 0x54 @1005 // identified description

  // remorrhaz helm
  COPY ~iu_iwd/itm/cdrelm.itm~  ~override~
    SAY 0x08 @1014 // unidentified name
    SAY 0x0c @1014 // identified name
    SAY 0x50 @1015 // unidentified description
    SAY 0x54 @1015 // identified description
  
  // remorrhaz shell
  COPY_EXISTING ~misc12.itm~ ~override/cdrem.itm~
    SAY 0x08 @1023 // unidentified name
    SAY 0x0c @1023 // identified name
    WRITE_ASCII 0x3a ~cdrem~  #8 // icon
    WRITE_ASCII 0x44 ~cdremg~ #8 // ground icon
    SAY 0x50 @1030 // unidentified description
    SAY 0x54 @1030 // identified description

  // white dragon scale armor
  COPY ~iu_iwd/itm/cdscale.itm~  ~override~
    SAY 0x08 @1010 // unidentified name
    SAY 0x0c @1010 // identified name
    SAY 0x50 @1011 // unidentified description
    SAY 0x54 @1011 // identified description
  
  // white dragon scales
  COPY ~iu_iwd/itm/cdscales.itm~ ~override~
    SAY 0x08 @1008 // unidentified name
    SAY 0x0c @1008 // identified name
    SAY 0x50 @1009 // unidentified description
    SAY 0x54 @1009 // identified description

  // improved bren muller's crossbow
  COPY_EXISTING ~lxbowbm.itm~ ~override/cdxbowbm.itm~
    WRITE_SHORT 0x42 0 // lore
    SAY 0x50 @1021  // unidentified description
    SAY 0x54 @1021  // identified description
    READ_ASCII  0x3a icon (8)
    READ_LONG   0x64 abil_off
    READ_SHORT  0x68 orig_abil_num
    READ_LONG   0x6a fx_off
    READ_SHORT  0x70 fx_num
    PATCH_IF orig_abil_num BEGIN
      READ_SHORT  (abil_off + 0x1e + ((orig_abil_num - 1) * 0x38)) last_fx_num
      READ_SHORT  (abil_off + 0x20 + ((orig_abil_num - 1) * 0x38)) last_fx_idx
      SET new_abil_fx_idx = last_fx_idx + last_fx_num
    END ELSE BEGIN
      SET new_abil_fx_idx = fx_num
    END
    // manually insert new ability
    INSERT_BYTES   (fx_off       ) 0x38            // new ability
      WRITE_BYTE   (fx_off       ) 3               // magical
      WRITE_BYTE   (fx_off + 0x01) 1               // ID to use
      WRITE_SHORT  (fx_off + 0x02) 3               // in item slots
      WRITE_ASCIIE (fx_off + 0x04) ~%icon%~ #8     // ability icon
      WRITE_SHORT (fx_off + 0x0c) 5                // target: caster
      WRITE_SHORT (fx_off + 0x0e) 1                // range
      WRITE_SHORT (fx_off + 0x20) new_abil_fx_idx  // fx index
      WRITE_SHORT (fx_off + 0x22) 1                // num charges: 1
      WRITE_BYTE  (fx_off + 0x24) 3                // recharges daily
      WRITE_BYTE  (fx_off + 0x27) 8                // after resting
      WRITE_SHORT (fx_off + 0x2a) 1                // projectile: none
    WRITE_SHORT 0x68 (THIS + 1)
    WRITE_LONG  0x6a (THIS + 0x38)
    LPF ADD_ITEM_EFFECT INT_VAR header = (orig_abil_num + 1) opcode = 142 target = 1 parameter2 = 17 duration = (14 - (2 * "%iwd-in-bg2%")) END // bless icon
    LPF ADD_ITEM_EFFECT INT_VAR header = (orig_abil_num + 1) opcode = 167 target = 1 parameter1 =  5 duration = (14 - (2 * "%iwd-in-bg2%")) END // +5 missile thac0

  // improved young rage
  COPY_EXISTING ~young.itm~  ~override/cdyoung.itm~
    WRITE_SHORT 0x42 0 // lore
    SAY 0x50 @1020  // unidentified description
    SAY 0x54 @1020  // identified description
    LPF DELETE_EFFECT INT_VAR check_globals = 0 header_type = 1 END // delete berserking effects on melee header

  // note for edion
  COPY_EXISTING ~misc78.itm~ ~override/cdyoungn.itm~
    SAY 0x08 @1040 // unidentified name
    SAY 0x0c @1040 // identified name
    SAY 0x50 @1041 // unidentified description
    SAY 0x54 @1041 // identified description
    
  // nerf existing cynicism a bit
  COPY_EXISTING ~cynicis.itm~ ~override~
    SAY 0x54 @1039 // identified description
    LPF ALTER_ITEM_HEADER INT_VAR header_type = 1 thac0_bonus = 2 END
    BUT_ONLY

  COPY_EXISTING ~emmeric.sto~ ~override~
    ADD_STORE_ITEM ~cdqsling~ AFTER ~xbow04~ #0 #0 #0 ~IDENTIFIED~ #1 // bag o' plenty
    ADD_STORE_ITEM ~cdqxbow~  AFTER ~xbow04~ #0 #0 #0 ~IDENTIFIED~ #1 // case o' plenty
    ADD_STORE_ITEM ~cdqbow~   AFTER ~xbow04~ #0 #0 #0 ~IDENTIFIED~ #1 // quiver o' plenty
    BUT_ONLY

  COPY ~iu_iwd/audio/cddaen.wav~ ~override~

  // vexed upgrade

  // if IWD-in-BG2 instead of true IWD...
  ACTION_IF ("%iwd-in-bg2%" = 1) THEN BEGIN
  
    COPY ~iu_iwd/dlg/vexing.d~     ~iu_iwd/dlg/vexing.d~
         ~iu_iwd/dlg/vexing_fp7.d~ ~iu_iwd/dlg/vexing_fp7.d~
      REPLACE_EVALUATE ~#\([0-9]+\)~ BEGIN
        SET "RESULT" = ("%MATCH1%" + 74100) // adjust strrefs
      END ~#%RESULT%~
  
    COPY ~iu_iwd/baf/cdvexcut.baf~ ~iu_iwd/baf/cdvexcut.baf~
      REPLACE_TEXTUALLY ~HideCreature(Myself,TRUE)~ 
        ~GiveItemCreate("mage06",Myself,1,1,1)  EquipItem("mage06")~
      REPLACE_TEXTUALLY ~ChangeAnimation("cdvex")~ 
        ~CreateCreature("cdvex",[-1.-1],0) DestroySelf()~

  END
  
  ACTION_IF dplanar_totlm >= 0 BEGIN
    COMPILE ~iu_iwd/dlg/vexing.d~
            ~iu_iwd/dlg/vexing_fp7.d~ EVALUATE_BUFFER
  END ELSE BEGIN
    COMPILE ~iu_iwd/dlg/vexing.d~
  END
  
  COMPILE ~iu_iwd/baf/cdvexcut.baf~
  
  COPY_EXISTING ~vexing.cre~ ~override/cdvex.cre~
    WRITE_ASCII 0x248 ~efdlgf~ #8
    WRITE_ASCII (0x334 - ("%iwd-in-bg2%" * 0x68)) ~cdvex~ #8
  
END
